//Задача выполнялась для предприятия ЖКХ
// Контекст 
// Платформа 1С:Предприятие 8.3. Конфигурация "1С:Учет в управляющих компаниях ЖКХ, ТСЖ и ЖСК"
// Расширение. Модуль http сервиса
// Фрагмент кода

#Область ОписаниеСервиса

Функция ОписаниеСервисаGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ТекстОтвета = "<h1> Описание сервиса </h1>";
	
	КорневойURL = Метаданные.HTTPСервисы.Расш1_ОбменЖККИТ.КорневойURL;
	
	ТекстОтвета = ТекстОтвета + ПолучитьТекстОтветаОписаниеСервиса(КорневойURL, "Собственники", "owners", ПолучитьВалидныеПараметрыДляСобственников());
			
	Ответ.УстановитьТелоИзСтроки(ТекстОтвета);
	Ответ.Заголовки.Вставить("Content-type", "text/html; charset=utf-8");
		
	Возврат Ответ;
	
КонецФункции


Функция ПолучитьТекстОтветаОписаниеСервиса(КорневойURL, НаименованиеДанных, Шаблон, ВалидныеПараметры, ИмяМетода="GET")
	
	ТекстОтвета = "<h3>"+ НаименованиеДанных + ": " + КорневойURL + "/"+ Шаблон + "</h3>";
	
	СсылкаНаСервис = "http://ИмяВебСервиса/ИмяБазы/hs/" + КорневойURL + "/"+ Шаблон;
	
	ТекстОтвета = ТекстОтвета + "<b>Пример полной ссылки:</b> " + "<a href=" + СсылкаНаСервис + ">" + СсылкаНаСервис + "</a>";
	ТекстОтвета = ТекстОтвета + "</br>";
	
	ТекстОтвета = ТекстОтвета + ДобавитьВТекстВалидныеПараметры(ВалидныеПараметры, ИмяМетода);	

	Возврат ТекстОтвета;
	
КонецФункции


Функция ДобавитьВТекстВалидныеПараметры(ВалидныеПараметры, ИмяМетода)
	
	Если ВалидныеПараметры = Неопределено Тогда
	    Возврат "";
	КонецЕсли;
	Текст = "<b>Параметры " + ИмяМетода + " запроса к сервису:</b> </br>";
	Для каждого Параметр Из ВалидныеПараметры Цикл
		Текст = Текст+ "&nbsp" + Параметр +  "</br>";
	КонецЦикла;	
	
	Возврат Текст;
	
КонецФункции 

#КонецОбласти


 #Область Собственники

Функция СобственникиGET(Запрос)
	
	НеВалидныйПараметр = ПолучитьНеВалидныйПараметр(Запрос.ПараметрыЗапроса, ПолучитьВалидныеПараметрыДляСобственников());
	Если ЗначениеЗаполнено(НеВалидныйПараметр) Тогда
		Возврат ПолучитьОтветНевалидныйПараметр(НеВалидныйПараметр);    
	КонецЕсли;	
	
	ДанныеОСобственниках = Расш1_ДанныеДляHTTPСервисовЖККИТ.ПолучитьДанныеОСобственниках(Запрос.ПараметрыЗапроса);	
	ДанныеОСобственникахJSON = Расш1_ДанныеДляHTTPСервисовЖККИТ.ПолучитьДанныеОСобственникахJSON(ДанныеОСобственниках);
	
	Возврат ПолучитьОтветСервера200(ДанныеОСобственникахJSON);
	
КонецФункции  


Функция ПолучитьВалидныеПараметрыДляСобственников()
	
	ВалидныеПараметры = Новый Массив; 
	
	ВалидныеПараметры.Добавить("code");
	ВалидныеПараметры.Добавить("owner_type");
	ВалидныеПараметры.Добавить("lastname");
	ВалидныеПараметры.Добавить("firstname");
	ВалидныеПараметры.Добавить("surname");
	ВалидныеПараметры.Добавить("phone");
	ВалидныеПараметры.Добавить("email");
	ВалидныеПараметры.Добавить("inn");	
	
	Возврат ВалидныеПараметры;
	
КонецФункции // ПолучитьКорректныеПараметрыОСобственниках()


#КонецОбласти


#Область ПроцедурыИФункцииОбщегоНазначения

Функция ПолучитьНеВалидныйПараметр(ПараметрыЗапроса, ВалидныеПараметры)
	
	Для каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл		
		Если ВалидныеПараметры.Найти(ПараметрЗапроса.Ключ) = Неопределено Тогда
			Возврат ПараметрЗапроса.Ключ;
		КонецЕсли;		
	КонецЦикла;	
	
	Возврат Неопределено;
	
КонецФункции 


Функция ПолучитьОтветНевалидныйПараметр(НеВалидныйПараметр)

	Ответ = Новый HTTPСервисОтвет(400);
	Ответ.УстановитьТелоИзСтроки("Неизвестный параметр запроса: " + НеВалидныйПараметр);
	Ответ.Заголовки.Вставить("Content-type", "application/json");	
	
	Возврат Ответ;
	
КонецФункции 


Функция ПолучитьОтветСервера200(Данные)

	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(Данные);
	Ответ.Заголовки.Вставить("Content-type", "application/json; charset=UTF-8");
	
	Возврат Ответ;

КонецФункции // ()

#КонецОбласти
